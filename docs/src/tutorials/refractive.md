```@meta
CurrentModule = ScatteringOptics
```

# Simulate Refractive Scattering
Another feature of `ScatteringOptics.jl` is simulating refractive scattering. This page gives a tutorial to simulate refractive scattering effects. 

## Loading your image
Again, we use an example image in [`eht-imaging`](https://github.com/achael/eht-imaging). Data can be downloaded from [here](data/jason_mad_eofn.fits). This is a general relativistic magnetohydrodynamic (GRMHD) model of the magnetic arrestic disk originally from [Dexter et al. 2020](https://ui.adsabs.harvard.edu/abs/2020MNRAS.494.4168D/abstract).

```@example 1
using CairoMakie
# This is the base package for the Skymodel of the Comrade.jl ecosystem
using VLBISkyModels
# Alternatively, you can import Comrade.jl instead. Either works.
# using Comrade

# Load a image model from an image FITS file
im = load_fits("data/jason_mad_eofn.fits", IntensityMap)

# Frequency of the image
νref = metadata(im).frequency
print("Frequency of the image: ", νref/1e9," GHz")

# Plot source image
imageviz(im, size=(600, 500), colormap=:afmhot)
```

## Simulating Refractive Scattering
And, then again initialize a scattering model.

```@example 1
using ScatteringOptics

# initialize the scattering model
sm = ScatteringModel()
```

Refractive scattering will distort the diffractively-scattered (i.e. emsemble-average) image and add compact substrucures so-called *refractive substructures*.
These effects will be simulated with a phase screen generated from the probabilistic magnetic field wander model of the intersteller medium implemented in the scattering model. 

First, let's initialize a phase screen model ([RefractivePhaseScreen](@ref)) from the scattering model and the model image.
```@example 1
# Initialize a refractive phase screen model from scattering and image models
rps = refractivephasescreen(sm, im) 

# Alternatively, you may make the screen model for arbitral grid
#   ScatteringOptics is design to work even if ScatteringScreen's grid is not consistent
#   with the image you want to scatter, thanks to a powerful interpolation scheme available.
# rps = RefractivePhaseScreen(sm, Nx, Ny, dx_rad, dy_rad) 
```

You can sample a Gaussian noise in the Fourier domain.

```@example 1
# Generate a phase screen. For this particular tutorial we will use StableRNG for the reproducibility.
using StableRNGs 
rng = StableRNG(123)
noise_screen = generate_gaussian_noise(rps; rng=rng)
```

This Gaussian noise will be scaled with the powerspectrum of the phase screen, and then transformed into the actual phase screen. The fully scattered image can be generated by [scatter_image](@ref) method.

```@example 1
# Produce the scattered image
im_a = scatter_image(rps, im; noise_screen=noise_screen)

# Plot source image
imageviz(im_a, size=(600, 500), colormap=:afmhot)
```

If you completely randomize the process, you can skip the step to generate `noise_screen` and specify it in the argument of [scatter_image](@ref) method. In this case, the screen will be automatically generated inside [scatter_image](@ref) method. 

There is a quick shortcut bypassing the noise screen generation, which has a less flexibility and a larger overhead for iterative processes.

```@example 1
# Produce scattered image with observing wavelength .13 cm
im_a2 = scatter_image(sm, im; rng=rng)

# Plot source image
imageviz(im_a2, size=(600, 500), colormap=:afmhot)
```

Just because the refractive scattering effects cannot be described analytically in Fourier domain, [scatter_image](@ref) method only works
for the image models (`::IntensityMap`). For sky models in `VLBISkyModels.jl`, you need to first instantiate an image model of your sky model.

Here we show an example using a simple Gaussian model. You need to create an image model with `intensitymap` method from `VLBISkyModels.jl`.

```@example 1
# Gaussian model from VLBISkyModels.jl
g = stretched(Gaussian(), μas2rad(5.0), μas2rad(5.0))

# Create an image model from the Gaussian model
im_g = intensitymap(g, imagepixels(μas2rad(200.0), μas2rad(200.0), 256, 256))

# Plot source image
imageviz(im_g, size=(600, 500), colormap=:afmhot)
```

Then you can make a scattered image. Don't forget to add the observing frequency as im_g doesn't have a frequency information.

```@example 1
# Produce scattered image. Don't forget to add the observing frequency as im_g doesn't have a frequency information.
im_ga = scatter_image(sm, im_g; rng=rng, νref=νref)

# Plot source image
imageviz(im_ga, size=(600, 500), colormap=:afmhot)
```