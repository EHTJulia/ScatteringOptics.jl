```@meta
CurrentModule = ScatteringOptics
```

# Simulate Diffractive Scattering
`ScatteringOptics.jl` is designed as a package inside the ecosystem of [`Comrade.jl`](https://github.com/ptiede/Comrade.jl). The scattering model in the package can scatter any sky model types from `Comrade.jl`.
This page describes how to simulate diffractive scattering.

## Loading your image
Here, we use an example image in [`eht-imaging`](https://github.com/achael/eht-imaging). Data can be downloaded from [here](data/jason_mad_eofn.fits). This is a general relativistic magnetohydrodynamic (GRMHD) model of the magnetic arrestic disk originally from [Dexter et al. 2020](https://ui.adsabs.harvard.edu/abs/2020MNRAS.494.4168D/abstract).

```@example 1
using CairoMakie
# This is the base package for the Skymodel of the Comrade.jl ecosystem
using VLBISkyModels
# Alternatively, you can import Comrade.jl instead. Either works.
# using Comrade

# Load a image model from an image FITS file
im = load_fits("data/jason_mad_eofn.fits", IntensityMap)

# Plot source image
imageviz(im, size=(600, 500), colormap=:afmhot)
```

You can instantiate your scattering model with `ScatteringModel()`. If nothing is specified, the model will use the best-fit parameter set for Sgr A* in [Johnson et al. 2018](https://ui.adsabs.harvard.edu/abs/2018ApJ...865..104J/abstract). 

```@example 1
using ScatteringOptics

# initialize the scattering model
sm = ScatteringModel()
```

You can change the parameters if you want to simulate a different scattering screen. See [ScatteringOptics.DipoleScatteringModel](@ref) for arguments. 

## Simulate diffractive scattering 
As explained in [Brief Introduction to Interstellar Scattering](@ref), diffractive scattering will cause angular broaderning of the resultant image, which is described by the convolution of the source image with a scattering kernel. `ScatteringOptics.jl` implements a `Comrade.jl`'s skymodel of the scattering kernel. You can generate it by 

```@example 1
# Frequency of the image
νref = metadata(im).frequency
print("Frequency of the image: ", νref/1e9," GHz")

# Create kernel from scattering model
skm = kernelmodel(sm, νref=νref)
```

`skm` is a subtype of `ComradeBase.AbstractModel`, and you can use any methods defined for sky models in `Comrade.jl`'s ecosystem. For instance, you can compute the ensemble-average (or diffractively scattered) image using the `convolve` method from `VLBISkyModels.jl`,

```@example 1
# scatter the image
im_ea = convolve(im, skm)

# Plot source image
imageviz(im_ea, size=(600, 500), colormap=:afmhot)
```

You can also directly convolve a skymodel from `ComradeBase.AbstractModel` and implemented in `VLBISkyModels.jl`.

```@example 1
# Gaussian model from VLBISkyModels.jl
g = stretched(Gaussian(), μas2rad(5.0), μas2rad(5.0))
im_g = intensitymap(g, imagepixels(μas2rad(200.0), μas2rad(200.0), 256, 256))

# Plot source image
imageviz(im_g, size=(600, 500), colormap=:afmhot)
```

Scattered model and image can be generated by,

```@example 1
g_ea = convolved(g, skm)
im_gea = intensitymap(g_ea, imagepixels(μas2rad(200.0), μas2rad(200.0), 256, 256))

# Plot source image
imageviz(im_gea, size=(600, 500), colormap=:afmhot)
```

You can also plot the scattering kernel. For instance in the image domain,

```@example 1
# make an image model of the scattering kernel
im_skm = intensitymap(skm, imagepixels(μas2rad(50.0), μas2rad(50.0), 256, 256))

# Plot source image
imageviz(im_skm, size=(600, 500), colormap=:afmhot)
```

You can compute the kernel visibility using `visibility_point` method from `VLBISkyModels.jl`. For instance,

```@example 1
# computing kernels from 0 to 10 Glambda along RA
u = LinRange(0,10e9,1000)
vis = [visibility_point(skm, (U=u, V=0, Fr=νref)) for u=u]

# Plot source image
f = Figure() 
ax = Axis(f[1, 1],
    xlabel="Baseline Length (Gλ)",
    ylabel="Kernel Amplitudes",
)
plot!(ax, u/1e9, abs.(vis))
f
```

## A quick shortcut
The above tutorial is intentionally written in a low level. There is [emsembleaverage](@ref) method to do a quick shortcut by bypassing the kernel generation.

```@example 1
# scatter the image
im_ea_2 = emsembleaverage(sm, im)

# Plot source image
imageviz(im_ea_2, size=(600, 500), colormap=:afmhot)
```

Although this is handy, it may have an extra overhead to initialize `skm` which may slow down highly iterative processes. 
[emsembleaverage](@ref) method also supports more general skymodels in `ComradeBase.AbstractModel` as an input instead of the image model.
